## <summary>policy for prometheusd</summary>

########################################
## <summary>
##	Create a set of derived types for various
##	prometheus exporters,
## </summary>
## <param name="prefix">
##	<summary>
##	The name to be used for deriving type names.
##	</summary>
## </param>
#
template(`prometheusd_exporter_template',`
#	gen_require(`
#		attribute prometheusd_exporter_domain;
#		type prometheusd_t;
#	')

	type $1_prometheusd_exporter_t;
	type $1_prometheusd_exporter_exec_t;
	init_daemon_domain($1_prometheusd_exporter_t, $1_prometheusd_exporter_exec_t)
	role system_r types $1_prometheusd_exporter_t;

')

########################################
## <summary>
##      Create a set of derived types for various
##      prometheus modules,
## </summary>
## <param name="prefix">
##      <summary>
##      The name to be used for deriving type names.
##      </summary>
## </param>
#
template(`prometheusd_module_template',`

        type $1_prometheusd_t;
        type $1_prometheusd_exec_t;
        init_daemon_domain($1_prometheusd_t, $1_prometheusd_exec_t)
        role system_r types $1_prometheusd_t;

	type $1_prometheusd_conf_t;
	files_config_file($1_prometheusd_conf_t)
	
	type $1_prometheusd_data_t;
	files_type($1_prometheusd_data_t)

')

########################################
## <summary>
##	Execute prometheusd_exec_t in the prometheusd domain.
## </summary>
## <param name="domain">
## <summary>
##	Domain allowed to transition.
## </summary>
## </param>
#
interface(`prometheusd_domtrans',`
	gen_require(`
		type prometheusd_t, prometheusd_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, prometheusd_exec_t, prometheusd_t)
')

######################################
## <summary>
##	Execute prometheusd in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`prometheusd_exec',`
	gen_require(`
		type prometheusd_exec_t;
	')

	corecmd_search_bin($1)
	can_exec($1, prometheusd_exec_t)
')

#######################################
## <summary>
##      Allow domain to connect to alertmananger port. Used for reverse proxy
## </summary>
## <param name="domain">
##      <summary>
##      Domain allowed access.
##      </summary>
## </param>
#
interface(`connect_alertmanager_prometheusd_port',`
	gen_require(`
		class tcp_socket name_connect;
		type alertmanager_prometheusd_port_t;
	')

	allow $1 alertmanager_prometheusd_port_t:tcp_socket name_connect;
')

#######################################
## <summary>
##	Read prometheus time series database.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`prometheusd_read_db',`
	gen_require(`
		type prometheusd_db_t;
	')

	files_search_usr($1)	
	read_files_pattern($1, prometheusd_db_t, prometheusd_db_t)
')

#######################################
## <summary>
##	Manage prometheusd time series database.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`prometheusd_manage_db',`
	gen_require(`
		type prometheusd_db_t;
	')

	files_search_usr($1)	
	manage_dirs_pattern($1, prometheusd_db_t, prometheusd_db_t)
	manage_files_pattern($1, prometheusd_db_t, prometheusd_db_t)
')

